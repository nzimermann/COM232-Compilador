#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = String
Parser = LL
#RegularDefinitions
inteiro: [0-9] | [1-9][0-9]+
letras: i | f | s | b
#Tokens
//Luis F. de Castilho, Arthur Felipe Lueders e Nicolas Zimermann

palavra_reservada: [a-z]+

identificador: _{letras} ( ( [a-z] [a-z0-9]* ( [A-Z] [a-z0-9]+ )* [A-Z]? ) | ( [A-Z] ( [a-z0-9] [A-Z]? )* ) )

constantei: {inteiro}

constantef: {inteiro} "." [0-9] ( [0-9]* [1-9] )?

constantes: \" ( [^\n \" "\" %] | % {letras} )* \"

: # [^\n]*

: "[" [^ "[" "]" ]* "]"

:[\s\t\n]

// palavras reservadas
//do else false fun if in main out repeat true while
do   = palavra_reservada: "do"
else = palavra_reservada: "else"
false = palavra_reservada: "false"
fun = palavra_reservada: "fun"
if = palavra_reservada: "if"
in = palavra_reservada: "in"
main = palavra_reservada: "main"
out = palavra_reservada: "out"
repeat = palavra_reservada: "repeat"
true = palavra_reservada: "true"
while = palavra_reservada: "while"

//simbolos especiais
// & | ! , ; = : ( ) { } == != < > + - * /
"&"
"|"
"!"
","
";"
"="
":"
"("
")"
"{"
"}"
"=="
"!="
"<"
">"
"+"
"-"
"*"
"/"
#NonTerminals
<programa>

<listadeinstrucoes>
<listadeinstrucoes1>
<instrucao>
<instrucao1>
//<declaracaodeconstante>
//<declaracaodevariaveis>
<comando>
<listadeidentificadores>
<listadeidentificadores1>
<valor>
<atribuicao>
<entradadedados>
<saidadedados>
<selecao>
<selecao1>
<repeticao>
<listadeentrada>
<listadeentrada1>
<parametroentrada>
<listadeexpressoes>
<listadeexpressoes1>
<listadecomandos>
<listadecomandos1>

<expressao>
<expressao_>
<elemento>
<relacional>
<relacional_>
<operador_relacional>
<aritmetica>
<aritmetica_>
<termo>
<termo_>
<fator>
#Grammar
<programa> ::= fun main "{" <listadeinstrucoes> "}" ; // ok

<listadeinstrucoes> ::= <instrucao> ";" <listadeinstrucoes1>; // ok

<listadeinstrucoes1> ::= î | <listadeinstrucoes> ; // ok

<instrucao> ::= <listadeidentificadores> <instrucao1> | <entradadedados> | <saidadedados> | <selecao> | <repeticao> ; // ok

<instrucao1> ::= î | ":" <valor> | "=" <expressao> ; // ok

//^^^^^^^^^^^^^^^^
//<instrucao> = A
//<declaracaodeconstante> = B
//<declaracaodevariaveis> = C
//<comando> = D
//<listadeidentificadores> = L
//<atribuicao> = V1
//<entradadedados> = V2
//<saidadedados> = V3
//<selecao> = V4
//<repeticao> = V5

//A -> B|C|D
//B -> L : valor
//C -> L
//D -> V1 | V2 | V3 | V4 | V5
//V1 -> L = exp

//A -> L : valor | L = exp | L | V2 | V3 | V4 | V5

//A -> L A1 | V2 | V3 | V4 | V5
//A1 -> : valor | = exp | î

<listadeidentificadores> ::= identificador <listadeidentificadores1>; // ok

<listadeidentificadores1> ::= î | "," <listadeidentificadores> ; // ok

<valor> ::= constantei | constantef | constantes | true | false ; // ok

<comando> ::= <atribuicao> | <entradadedados> | <saidadedados> | <selecao> | <repeticao> ; // ok

<atribuicao> ::= <listadeidentificadores> "=" <expressao> ; // ok

<entradadedados> ::= in "(" <listadeentrada> ")" ; // ok

<parametroentrada> ::= constantes "," <listadeidentificadores> | <listadeidentificadores> ; // ok

<listadeentrada> ::= <parametroentrada> <listadeentrada1> ; // ok

<listadeentrada1> ::= î | ";" <listadeentrada> ; // ok

<saidadedados> ::= out "(" <listadeexpressoes> ")" ; // ok

<listadeexpressoes> ::= <expressao> <listadeexpressoes1> ; // ok

<listadeexpressoes1> ::= î |  "," <listadeexpressoes> ; // ok

<selecao> ::= if "(" <expressao> ")" "{" <listadecomandos> "}" <selecao1> ; // ok

<selecao1> ::= î | else "{" <listadecomandos> "}" ; // ok

<listadecomandos> ::= <comando> ";" <listadecomandos1>; // ok

<listadecomandos1> ::= î | <listadecomandos> ; // ok

<repeticao> ::= while "(" <expressao> ")" do "{" <listadecomandos> "}"
		| repeat "{" <listadecomandos> "}" while "(" <expressao> ")" ; // ok

// =========TRABALHO 3===========

<expressao> ::= <elemento> <expressao_> ;

<expressao_> ::= î | "&" <elemento> <expressao_> | "|" <elemento> <expressao_> ;

<elemento> ::= <relacional> | true | false | "!" <elemento> ;

<relacional> ::= <aritmetica> <relacional_> ;

<relacional_> ::= î | <operador_relacional> <aritmetica> ;

<operador_relacional> ::= "==" | "!=" | "<" | ">" ;

<aritmetica> ::= <termo> <aritmetica_> ;

<aritmetica_> ::= î | "+" <termo> <aritmetica_> | "-" <termo> <aritmetica_> ;

<termo> ::= <fator> <termo_> ;
<termo_> ::= î | "*" <fator> <termo_> | "/" <fator> <termo_> ;
<fator> ::=	identificador |
		constantei |
		constantef |
		constantes |
		"(" <expressao> ")" |
		"+" <fator> |
		"-" <fator> ;



