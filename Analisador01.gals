#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = String
Parser = LL
#RegularDefinitions
inteiro: [0-9] | [1-9][0-9]+
letras: i | f | s | b
#Tokens
//Luis F. de Castilho, Arthur Felipe Lueders e Nicolas Zimermann

palavra_reservada: [a-z]+

identificador: _{letras} ( ( [a-z] [a-z0-9]* ( [A-Z] [a-z0-9]+ )* [A-Z]? ) | ( [A-Z] ( [a-z0-9] [A-Z]? )* ) )

constantei: {inteiro}

constantef: {inteiro} "." [0-9] ( [0-9]* [1-9] )?

constantes: \" ( [^\n \" "\" %] | % {letras} )* \"

: # [^\n]*

: "[" [^ "[" "]" ]* "]"

:[\s\t\n]

// palavras reservadas
//do else false fun if in main out repeat true while
do   = palavra_reservada: "do"
else = palavra_reservada: "else"
false = palavra_reservada: "false"
fun = palavra_reservada: "fun"
if = palavra_reservada: "if"
in = palavra_reservada: "in"
main = palavra_reservada: "main"
out = palavra_reservada: "out"
repeat = palavra_reservada: "repeat"
true = palavra_reservada: "true"
while = palavra_reservada: "while"

//simbolos especiais
// & | ! , ; = : ( ) { } == != < > + - * /
"&"
"|"
"!"
","
";"
"="
":"
"("
")"
"{"
"}"
"=="
"!="
"<"
">"
"+"
"-"
"*"
"/"
#NonTerminals
<programa>

<listadeinstrucoes>
<listadeinstrucoes1>
<instrucao>
<instrucao1>
<comando>
<listadeidentificadores>
<listadeidentificadores1>
<valor>
<atribuicao>
<entradadedados>
<saidadedados>
<selecao>
<selecao1>
<repeticao>
<listadeentrada>
<listadeentrada1>
<parametroentrada>
<listadeexpressoes>
<listadeexpressoes1>
<listadecomandos>
<listadecomandos1>

<expressao>
<expressao_>
<elemento>
<relacional>
<relacional_>
<operador_relacional>
<aritmetica>
<aritmetica_>
<termo>
<termo_>
<fator>
#Grammar
<programa> ::= fun main "{" <listadeinstrucoes> "}" ;

<listadeinstrucoes> ::= <instrucao> ";" <listadeinstrucoes1>;

<listadeinstrucoes1> ::= � | <listadeinstrucoes> ;

<instrucao> ::= <listadeidentificadores> <instrucao1> | <entradadedados> | <saidadedados> | <selecao> | <repeticao> ;

<instrucao1> ::= � | ":" <valor> | "=" <expressao> ;


<listadeidentificadores> ::= identificador <listadeidentificadores1>; 

<listadeidentificadores1> ::= � | "," <listadeidentificadores> ;

<valor> ::= constantei | constantef | constantes | true | false ;

<comando> ::= <atribuicao> | <entradadedados> | <saidadedados> | <selecao> | <repeticao> ; // ok

<atribuicao> ::= <listadeidentificadores> "=" <expressao> ;

<entradadedados> ::= in "(" <listadeentrada> ")" ; 

<parametroentrada> ::= constantes "," <listadeidentificadores> | <listadeidentificadores> ;

<listadeentrada> ::= <parametroentrada> <listadeentrada1> ;

<listadeentrada1> ::= � | ";" <listadeentrada> ;

<saidadedados> ::= out "(" <listadeexpressoes> ")" ;

<listadeexpressoes> ::= <expressao> <listadeexpressoes1> ;

<listadeexpressoes1> ::= � |  "," <listadeexpressoes> ;

<selecao> ::= if "(" <expressao> ")" "{" <listadecomandos> "}" <selecao1> ;

<selecao1> ::= � | else "{" <listadecomandos> "}" ;

<listadecomandos> ::= <comando> ";" <listadecomandos1>;

<listadecomandos1> ::= � | <listadecomandos> ;

<repeticao> ::= while "(" <expressao> ")" do "{" <listadecomandos> "}"
		| repeat "{" <listadecomandos> "}" while "(" <expressao> ")" ;

<expressao> ::= <elemento> <expressao_> ;

<expressao_> ::= � | "&" <elemento> <expressao_> | "|" <elemento> <expressao_> ;

<elemento> ::= <relacional> | true | false | "!" <elemento> ;

<relacional> ::= <aritmetica> <relacional_> ;

<relacional_> ::= � | <operador_relacional> <aritmetica> ;

<operador_relacional> ::= "==" | "!=" | "<" | ">" ;

<aritmetica> ::= <termo> <aritmetica_> ;

<aritmetica_> ::= � | "+" <termo> <aritmetica_> | "-" <termo> <aritmetica_> ;

<termo> ::= <fator> <termo_> ;
<termo_> ::= � | "*" <fator> <termo_> | "/" <fator> <termo_> ;
<fator> ::=	identificador |
		constantei |
		constantef |
		constantes |
		"(" <expressao> ")" |
		"+" <fator> |
		"-" <fator> ;



